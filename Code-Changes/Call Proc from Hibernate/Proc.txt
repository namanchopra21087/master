USE [OFSDB]
GO
/****** Object:  StoredProcedure [Txn].[Proc_Details]  ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE  [Txn].[Proc_Details]
@VOYAGE_NUMBER VARCHAR(5)
, @Type Varchar(50)
, @VESSEL_NUMBER NUMERIC(10)
, @Debtor_Code VARCHAR(50) = '700-G001'
, @Debtor_Name VARCHAR(128) = 'Company Name'
, @Acc_Code VARCHAR(50) = '100-0100'

AS
-- =============================================
-- Author:		Naman Chopra
-- Sample Call : EXEC Txn.Proc_Details '018' , 
-- Sample Call : EXEC Txn.Proc_Details '018' ,
-- Sample Call : EXEC Txn.Proc_Details '018' , 

-- Sample call : EXEC Txn.Proc_Details '057' ,
-- =============================================
BEGIN
	BEGIN TRY
		SET NOCOUNT ON;
		
		-- Invoice Details Taken Here
		SELECT INV.INVOICE_ID, SUBSTRING(INV.INVOICE_NUMBER, 3 ,LEN(INV.INVOICE_NUMBER)) INVOICE_NUMBER  , INV.INVOICE_DATE, @Debtor_Code [DEBTOR_CODE]
		, @Debtor_Name [DEBTOR_NAME], SUM(CASE WHEN MSUO.SETUP_CODE = 'M3' THEN QUANTITY ELSE NUMBER_OF_UNITS END ) TOTAL_UNITS, '' [AGENT_CODE]
		, 30 [TERM_CODE], ISNULL(C.CUSTOMER_NAME, '')	DESCRIPTION, ISNULL(DP.PORT_NAME, '') DESCRIPTION2, ISNULL(SI.SI_NUMBER, '') SI_NUMBER, SS.VOYAGE_NUMBER PROJECT_CODE
		, '' [DEPARTMENT], CASE WHEN CUR.SETUP_CODE = 'MYR' THEN 'RM' ELSE CUR.SETUP_CODE END CURRENCY, INV.EXCHANGE_RATE CURRENCY_RATE
		, CASE WHEN INVTS.TF_STATUS = 'CANCELLED' THEN 'Y' ELSE 'N' END CANCELLED, @Acc_Code [ACC_CODE], MSCC.SETUP_DESCRIPTION [ITEM_DESCRIPTION] 
		, '' [ITEM_PROJ], '' [ITEM_DEP], MSBL.SETUP_DESCRIPTION BUSINESS_LINE, 'LINER INVOICE' INV_TYPE, ISNULL(V.VESSEL_NAME, '') VESSEL_NAME
		, ISNULL(LP.PORT_NAME, '') LOADING_PORT, ID.CHARGE_CODE_MASTER_ID, ID.SI_ID, ID.CARGO_TYPE_MASTER_ID, MSCT.SETUP_DESCRIPTION CARGO_TYPE
		
		INTO #Invoice_Data
		FROM Txn.INVOICE INV 
		INNER JOIN Txn.SHIPPING_SCHEDULE SS ON INV.SHIPPING_SCHEDULE_ID = SS.SHIPPING_SCHEDULE_ID
		INNER JOIN Txn.INVOICE_DETAIL ID ON INV.INVOICE_ID = ID.INVOICE_ID
		INNER JOIN Master.MASTER_SETUP CUR ON INV.CURRENCY_MASTER_ID = CUR.MASTER_ID
		INNER JOIN Txn.TRANSACTION_STATUS INVTS ON INV.TF_STATUS_ID = INVTS.TF_STATUS_ID
		INNER JOIN Master.MASTER_SETUP MSCC ON ID.CHARGE_CODE_MASTER_ID = MSCC.MASTER_ID
		INNER JOIN Master.MASTER_SETUP MSBL ON INV.BUSINESS_LINE_MASTER_ID = MSBL.MASTER_ID
		INNER JOIN Master.PORT LP ON SS.LOAD_PORT_ID = LP.PORT_ID
		INNER JOIN Master.MASTER_SETUP MSUO ON ID.UOM_MASTER_ID = MSUO.MASTER_ID
		LEFT JOIN Master.CUSTOMER C ON INV.LINER_ACTUAL_BILLING_CUSTOMER_ID = C.CUSTOMER_ID
		LEFT JOIN Master.PORT DP ON ID.DESTINATION_PORT_ID = DP.PORT_ID
		LEFT JOIN Master.VESSEL V ON SS.VESSEL_ID = V.VESSEL_ID
		LEFT JOIN Txn.SHIPPING_INSTRUCTION SI ON ID.SI_ID = SI.SI_ID 
		LEFT JOIN Master.MASTER_SETUP MSCT ON ID.CARGO_TYPE_MASTER_ID = MSCT.MASTER_ID
		WHERE SS.VOYAGE_NUMBER = @VOYAGE_NUMBER AND INV.IS_LINER_INVOICE = 1  AND SS.VESSEL_ID=@VESSEL_NUMBER
		AND  MSCC.SETUP_DESCRIPTION IN ('OCEAN FREIGHT', 'BUNKER SURCHARGE','GS-OCEAN FREIGHT - 30 TON AND BELOW SLAB')
		GROUP BY INV.INVOICE_ID, INV.INVOICE_NUMBER  , INV.INVOICE_DATE, SI.SI_NUMBER, SS.VOYAGE_NUMBER, C.CUSTOMER_NAME, DP.PORT_NAME, CUR.SETUP_CODE
		, INV.EXCHANGE_RATE, INVTS.TF_STATUS, MSCC.SETUP_DESCRIPTION, MSBL.SETUP_DESCRIPTION, V.VESSEL_NAME, LP.PORT_NAME, ID.CHARGE_CODE_MASTER_ID, ID.SI_ID
		, ID.CARGO_TYPE_MASTER_ID, MSCT.SETUP_DESCRIPTION
		
		
		-- Invoice TAX Details Taken Here
		SELECT INV.INVOICE_ID, MST.SETUP_CODE TAX_CODE, SUM(LINE_AMT_WOT_TAX_BASECURR) INVOICE_AMOUNT, SUM(LINE_TAX_AMT_BASECURR) TAX_AMOUNT, TM.TAX_PERCENTAGE TAX_RATE
		, MST.SETUP_DESCRIPTION TAX_DESC, ID.CHARGE_CODE_MASTER_ID, ID.SI_ID, ID.CARGO_TYPE_MASTER_ID, MSCT.SETUP_DESCRIPTION CARGO_TYPE
		INTO #Invoice_Tax_Data
		FROM Txn.INVOICE_DETAIL ID
		INNER JOIN Txn.INVOICE INV ON ID.INVOICE_ID = INV.INVOICE_ID
		INNER JOIN Master.MASTER_SETUP MSCC ON ID.CHARGE_CODE_MASTER_ID = MSCC.MASTER_ID
		INNER JOIN Txn.SHIPPING_SCHEDULE SS ON INV.SHIPPING_SCHEDULE_ID = SS.SHIPPING_SCHEDULE_ID
		LEFT JOIN Master.TAX_MASTER TM ON ID.TAX_MASTER_ID = TM.TAX_MASTER_ID
		LEFT JOIN Master.MASTER_SETUP MST ON TM.TAX_CODE_MASTER_ID = MST.MASTER_ID
		LEFT JOIN Master.MASTER_SETUP MSCT ON ID.CARGO_TYPE_MASTER_ID = MSCT.MASTER_ID
		WHERE SS.VOYAGE_NUMBER = @VOYAGE_NUMBER AND INV.IS_LINER_INVOICE = 1 AND SS.VESSEL_ID=@VESSEL_NUMBER
		AND  MSCC.SETUP_DESCRIPTION IN ('OCEAN FREIGHT', 'BUNKER SURCHARGE','GS-OCEAN FREIGHT - 30 TON AND BELOW SLAB')
		GROUP BY MST.SETUP_DESCRIPTION, TM.TAX_PERCENTAGE, INV.INVOICE_ID, MST.SETUP_CODE, ID.CHARGE_CODE_MASTER_ID, ID.SI_ID
		, ID.CARGO_TYPE_MASTER_ID, MSCT.SETUP_DESCRIPTION

		-- CNDN Details Taken Here		
		SELECT  CD.CD_NOTE_ID, SUBSTRING(CD.CD_NOTE_NUMBER, 3 ,LEN(CD.CD_NOTE_NUMBER)) CD_NOTE_NUMBER, CD.CD_NOTE_DATE, @Debtor_Code [DEBTOR_CODE]
		, @Debtor_Name [DEBTOR_NAME], SUM(CDD.NUMBER_OF_UNITS) TOTAL_UNITS, '' [AGENT_CODE], 30 [TERM_CODE], ISNULL(C.CUSTOMER_NAME, '')	DESCRIPTION
		, ISNULL(DP.PORT_NAME, '') DESCRIPTION2, INV.INVOICE_NUMBER SHIPMENT_NUMBER, SS.VOYAGE_NUMBER PROJECT_CODE, '' [Department]
		, CASE WHEN CUR.SETUP_CODE ='MYR' THEN 'RM' ELSE CUR.SETUP_CODE END CURRENCY, INV.EXCHANGE_RATE CURRENCY_RATE
		, CASE WHEN CDTS.TF_STATUS = 'CANCELLED' THEN 'Y' ELSE 'N' END CANCELLED, @Acc_Code [ACC_CODE]
		, MSCC.SETUP_DESCRIPTION [ITEM_DESCRIPTION] , '' [ITEM_PROJ], '' [ITEM_DEP], MSBL.SETUP_DESCRIPTION BUSINESS_LINE
		, CASE WHEN CD.CREDIT_DEBIT_FLAG = 'CN' THEN 'LINER CREDIT NOTE' ELSE 'LINER DEBIT NOTE' END INV_TYPE
		, ISNULL(V.VESSEL_NAME, '') VESSEL_NAME, ISNULL(LP.PORT_NAME, '') LOADING_PORT, CDD.CHARGE_CODE_MASTER_ID
		, CDD.CARGO_TYPE_MASTER_ID, MSCT.SETUP_DESCRIPTION CARGO_TYPE
		INTO #CNDN_Data
		FROM Txn.CREDIT_DEBIT_NOTE CD 
		INNER JOIN Txn.INVOICE INV ON CD.INVOICE_ID = INV.INVOICE_ID
		INNER JOIN Txn.SHIPPING_SCHEDULE SS ON INV.SHIPPING_SCHEDULE_ID = SS.SHIPPING_SCHEDULE_ID
		INNER JOIN Txn.CD_NOTE_DETAIL CDD ON CD.CD_NOTE_ID = CDD.CD_NOTE_ID
		INNER JOIN Master.MASTER_SETUP CUR ON CD.CURRENCY_MASTER_ID = CUR.MASTER_ID
		INNER JOIN Txn.TRANSACTION_STATUS CDTS ON CD.TF_STATUS_ID = CDTS.TF_STATUS_ID
		INNER JOIN Master.MASTER_SETUP MSCC ON CDD.CHARGE_CODE_MASTER_ID = MSCC.MASTER_ID
		INNER JOIN Master.MASTER_SETUP MSBL ON CD.BUSINESS_LINE_MASTER_ID = MSBL.MASTER_ID
		INNER JOIN Master.MASTER_SETUP MSUO ON CDD.UOM_MASTER_ID = MSUO.MASTER_ID
		LEFT JOIN Master.CUSTOMER C ON INV.LINER_ACTUAL_BILLING_CUSTOMER_ID = C.CUSTOMER_ID
		LEFT JOIN Master.PORT DP ON CDD.DESTINATION_PORT_ID = DP.PORT_ID
		LEFT JOIN Master.PORT LP ON CDD.LOADING_PORT_ID = LP.PORT_ID
		LEFT JOIN Master.VESSEL V ON SS.VESSEL_ID = V.VESSEL_ID
		LEFT JOIN Master.MASTER_SETUP MSCT ON CDD.CARGO_TYPE_MASTER_ID = MSCT.MASTER_ID
		WHERE SS.VOYAGE_NUMBER = @VOYAGE_NUMBER AND INV.IS_LINER_INVOICE = 1 	AND SS.VESSEL_ID=@VESSEL_NUMBER
		GROUP BY CD.CD_NOTE_ID, CD.CD_NOTE_NUMBER, CD.CD_NOTE_DATE, INV.INVOICE_NUMBER, SS.VOYAGE_NUMBER, C.CUSTOMER_NAME, DP.PORT_NAME, CUR.SETUP_CODE, INV.EXCHANGE_RATE
		, CDTS.TF_STATUS, MSCC.SETUP_DESCRIPTION, MSBL.SETUP_DESCRIPTION, CD.CREDIT_DEBIT_FLAG, V.VESSEL_NAME, LP.PORT_NAME, CDD.CHARGE_CODE_MASTER_ID
		, CDD.CARGO_TYPE_MASTER_ID, MSCT.SETUP_DESCRIPTION

		-- CNDN TAX Details Taken Here
		SELECT CD.CD_NOTE_ID, MST.SETUP_CODE TAX_CODE, SUM(LINE_AMT_WOT_TAX_BASECURR) CNDN_AMOUNT, SUM(LINE_TAX_AMT_BASECURR) TAX_AMOUNT, TM.TAX_PERCENTAGE TAX_RATE
		, MST.SETUP_DESCRIPTION TAX_DESC, CDD.CHARGE_CODE_MASTER_ID, CDD.CARGO_TYPE_MASTER_ID, MSCT.SETUP_DESCRIPTION 
		INTO #CNDN_Tax_Data
		FROM Txn.CD_NOTE_DETAIL CDD
		INNER JOIN Txn.CREDIT_DEBIT_NOTE CD ON CDD.CD_NOTE_ID = CD.CD_NOTE_ID
		INNER JOIN Txn.INVOICE INV ON CD.INVOICE_ID = INV.INVOICE_ID
		INNER JOIN Txn.SHIPPING_SCHEDULE SS ON INV.SHIPPING_SCHEDULE_ID = SS.SHIPPING_SCHEDULE_ID
		INNER JOIN Master.MASTER_SETUP MSCC ON CDD.CHARGE_CODE_MASTER_ID = MSCC.MASTER_ID
		LEFT JOIN Master.TAX_MASTER TM ON CDD.TAX_MASTER_ID = TM.TAX_MASTER_ID
		LEFT JOIN Master.MASTER_SETUP MST ON TM.TAX_CODE_MASTER_ID = MST.MASTER_ID
		LEFT JOIN Master.MASTER_SETUP MSCT ON CDD.CARGO_TYPE_MASTER_ID = MSCT.MASTER_ID
		WHERE SS.VOYAGE_NUMBER = @VOYAGE_NUMBER AND INV.IS_LINER_INVOICE = 1 AND SS.VESSEL_ID=@VESSEL_NUMBER
		GROUP BY MST.SETUP_DESCRIPTION, TM.TAX_PERCENTAGE, CD.CD_NOTE_ID, MST.SETUP_CODE, CDD.CHARGE_CODE_MASTER_ID
		, CDD.CARGO_TYPE_MASTER_ID, MSCT.SETUP_DESCRIPTION
		
		SELECT  ID.INVOICE_ID, ID.INVOICE_NUMBER, CONVERT(VARCHAR(10), ID.INVOICE_DATE) INVOICE_DATE, ID.DEBTOR_CODE, ID.DEBTOR_NAME, ID.TOTAL_UNITS
			, ID.AGENT_CODE, ID.TERM_CODE, ID.DESCRIPTION, ID.DESCRIPTION2, ID.SI_NUMBER, ID.PROJECT_CODE, ID.DEPARTMENT, ID.CURRENCY, ID.CURRENCY_RATE
			, ID.CANCELLED, ID.ACC_CODE, ID.ITEM_DESCRIPTION, ID.ITEM_PROJ, ID.ITEM_DEP, ITD.INVOICE_AMOUNT, ITD.TAX_CODE, ITD.TAX_AMOUNT, ITD.TAX_RATE
			, ID.BUSINESS_LINE, ID.INV_TYPE, ID.VESSEL_NAME, ID.LOADING_PORT, ITD.TAX_DESC, ID.CARGO_TYPE
			INTO #Final_Data
			 FROM #Invoice_Data ID 
			 INNER JOIN #Invoice_Tax_Data ITD ON ID.INVOICE_ID = ITD.INVOICE_ID AND ID.CHARGE_CODE_MASTER_ID = ITD.CHARGE_CODE_MASTER_ID AND
			 ISNULL(ID.SI_ID, 0) =  ISNULL(ITD.SI_ID, 0) AND ISNULL(ID.CARGO_TYPE_MASTER_ID, 0) = ISNULL(ITD.CARGO_TYPE_MASTER_ID, 0)
		UNION ALL
		SELECT  ID.CD_NOTE_ID, ID.CD_NOTE_NUMBER, CONVERT(VARCHAR(10), ID.CD_NOTE_DATE) INVOICE_DATE, ID.DEBTOR_CODE, ID.DEBTOR_NAME, ID.TOTAL_UNITS
			, ID.AGENT_CODE, ID.TERM_CODE, ID.DESCRIPTION, ID.DESCRIPTION2, ID.SHIPMENT_NUMBER, ID.PROJECT_CODE, ID.DEPARTMENT, ID.CURRENCY, ID.CURRENCY_RATE
			, ID.CANCELLED, ID.ACC_CODE, ID.ITEM_DESCRIPTION, ID.ITEM_PROJ, ID.ITEM_DEP, ITD.CNDN_AMOUNT, ITD.TAX_CODE, ITD.TAX_AMOUNT, ITD.TAX_RATE
			, ID.BUSINESS_LINE, ID.INV_TYPE, ID.VESSEL_NAME, ID.LOADING_PORT, ITD.TAX_DESC, ID.CARGO_TYPE
			 FROM #CNDN_Data ID 
			 INNER JOIN #CNDN_Tax_Data ITD ON ID.CD_NOTE_ID = ITD.CD_NOTE_ID AND ID.CHARGE_CODE_MASTER_ID = ITD.CHARGE_CODE_MASTER_ID AND
			 ISNULL(ID.CARGO_TYPE_MASTER_ID, 0) = ISNULL(ITD.CARGO_TYPE_MASTER_ID, 0)
		
		UPDATE #Final_Data SET INVOICE_AMOUNT = 0 , TAX_AMOUNT = 0 WHERE CANCELLED = 'Y'

		SELECT * FROM #Final_Data Where INV_TYPE =  @Type

		IF OBJECT_ID('TEMPDB..#Invoice_Data') IS NOT NULL
				DROP TABLE #Invoice_Data
		IF OBJECT_ID('TEMPDB..#Invoice_Tax_Data') IS NOT NULL
				DROP TABLE #Invoice_Tax_Data
		IF OBJECT_ID('TEMPDB..#CNDN_Data') IS NOT NULL
				DROP TABLE #CNDN_Data
		IF OBJECT_ID('TEMPDB..#CNDN_Tax_Data') IS NOT NULL
				DROP TABLE #CNDN_Tax_Data
		IF OBJECT_ID('TEMPDB..#Final_Data') IS NOT NULL
				DROP TABLE #Final_Data

	END TRY

	BEGIN CATCH

		IF OBJECT_ID('TEMPDB..#Invoice_Data') IS NOT NULL
				DROP TABLE #Invoice_Data
		IF OBJECT_ID('TEMPDB..#Invoice_Tax_Data') IS NOT NULL
				DROP TABLE #Invoice_Tax_Data
		IF OBJECT_ID('TEMPDB..#CNDN_Data') IS NOT NULL
				DROP TABLE #CNDN_Data
		IF OBJECT_ID('TEMPDB..#CNDN_Tax_Data') IS NOT NULL
				DROP TABLE #CNDN_Tax_Data
		IF OBJECT_ID('TEMPDB..#Final_Data') IS NOT NULL
				DROP TABLE #Final_Data

		SELECT 'Procedure: ' + ERROR_PROCEDURE() + ' ErrorMessage: ' + ERROR_MESSAGE()
	END CATCH		 
END

